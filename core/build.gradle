apply plugin: 'com.android.library'
apply plugin: 'bintray-release'
apply plugin: 'jacoco'

android {
    compileSdkVersion 24
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        lintConfig file("$rootProject.projectDir/team-props/lint-rules/config.xml")
        abortOnError false
    }

    aaptOptions {
        cruncherEnabled = false
    }
}

dependencies {
    compile 'com.novoda:notils:2.2.16'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.9'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'

    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}

project.afterEvaluate({
    task jarTest(type: Jar, dependsOn: assembleDebugUnitTest) {
        from "$project.buildDir/intermediates/classes/test/debug/"
        classifier = 'test'
    }

    configurations {
        testOutput.extendsFrom(testCompile)
    }

    artifacts {
        testOutput jarTest
    }
})

publish {
    userOrg = 'novoda'
    repoName = 'maven-private'
    groupId = 'com.novoda'
    artifactId = 'no-player'
    version = project.version

    uploadName = 'no-player'
    description = 'player to wrap players'
    website = 'https://github.com/novoda/no-player'
}
